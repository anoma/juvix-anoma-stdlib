module Anoma.Transaction;

import Stdlib.Prelude open;

module Kind;

  module Private;
    type Kind := mk {id : Nat};

    instance
    Kind-Eq : Eq Kind :=
      mkEq@{
        eq (d1 d2 : Kind) : Bool := Kind.id d1 == Kind.id d2
      };

    instance
    Kind-Ord : Ord Kind :=
      mkOrd@{
        cmp (d1 d2 : Kind) : Ordering :=
          Ord.cmp (Kind.id d1) (Kind.id d2)
      };

  end;

  open Private using {Kind} public;

  syntax alias Eq := Private.Kind-Eq;

end;

open Kind using {Kind} public;

open Kind.Private;

module DeltaComponent;

  type DeltaComponent :=
    mk {
      denom : Nat;
      sign : Bool;
      amount : Nat
    };

  zero : DeltaComponent :=
    mk@{
      denom := 0;
      sign := true;
      amount := 0
    };

  open DeltaComponent public;
end;

open DeltaComponent using {DeltaComponent} public;

Delta : Type := List DeltaComponent;

-- Proof, Resource, and Transaction are mutually recursive so they must be
-- defined in the same module.
module Helper;
  syntax alias Proof := Resource;
  syntax alias Commitment := Nat;
  syntax alias Nullifier := Nat;

  positive
  type Resource :=
    mkResource {
      logic : Resource -> Transaction -> Bool;
      -- TODO: Should be String
      label : Nat;
      quantity : Nat;
      data : Nat;
      eph : Bool;
      nonce : Nat;
      npk : Nat;
      rseed : Nat
    };

  type Transaction :=
    mkTransaction {
      roots : List Nat;
      commitments : List Commitment;
      nullifiers : List Nullifier;
      proofs : List Proof;
      delta : Delta;
      extra : Nat;
      -- TODO: Encoding of the preference function is not specified
      preference : Nat
    };
end;

module Transaction;
  syntax alias mk := Helper.mkTransaction;

  open Helper.Transaction public;
  open Helper using {Transaction} public;
end;

open Transaction using {Transaction} public;

module Resource;
  syntax alias mk := Helper.mkResource;

  open Helper.Resource public;
  open Helper using {Resource} public;
end;

open Resource using {Resource} public;
